pkgname=autoconf
pkgver=2.69
pkgrel=3
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=()
checkdepends=('gcc-fortran')
install=autoconf.install
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=(
  '64ebcec9f8ac5b2487125a86a7760d2591ac9e1d3dbd59489633f9de62a57684'
  'SKIP'
)
validpgpkeys=('71C2CC22B1C4602927D2F3AAA7A16B4A2527436A')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  perl -pi -e 's/libtoolize/glibtoolize/g' bin/autoreconf.in
  perl -pi -e 's/libtoolize/glibtoolize/g' man/autoreconf.1
  ./configure --prefix=/opt/arch
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # license exception
  ginstall -d -m755 $pkgdir/opt/arch/share/licenses/autoconf
  ginstall -m644 COPYING.EXCEPTION \
    $pkgdir/opt/arch/share/licenses/autoconf/COPYING.EXCEPTION

  # remove unwanted file
  rm -f ${pkgdir}/opt/arch/share/info/standards.info
}
