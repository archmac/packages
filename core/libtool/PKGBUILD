pkgname=libtool
pkgver=2.4.6
pkgrel=3
pkgdesc="A generic library support script"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
groups=('base-devel')
provides=("libltdl=$pkgver")
conflicts=('libltdl')
replaces=('libltdl')
install=libtool.install
source=(https://ftp.gnu.org/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=(
  '7c87a8c2c8c0fc9cd5019e402bed4292462d00a718a7cd5f11218153bf28b26f'
  'SKIP'
)
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')   # Gary Vaughan

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/opt/arch --program-prefix=g --enable-ltdl-install
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install
}
