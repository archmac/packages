pkgname=bash
_basever=4.4
_patchlevel=012
pkgver=$_basever.$_patchlevel
pkgrel=2
pkgdesc='The GNU Bourne Again shell'
arch=('x86_64')
license=('GPL')
url='http://www.gnu.org/software/bash/bash.html'
groups=('base')
backup=()
depends=()
optdepends=('bash-completion: for tab completion')
provides=('sh')
install=bash.install
source=(http://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig})
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p){,.sig})
  done
fi

prepare() {
  cd $pkgname-$_basever

  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    msg "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
  done
}

build() {
  cd $pkgname-$_basever

  #_bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/opt/arch/sbin:/opt/arch/bin:/usr/bin\"\'
  #             -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
  #             -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
  #             -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\')
  #export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure --prefix=/opt/arch \
              --with-curses --enable-readline \
              --without-bash-malloc --with-installed-readline
  make
}

check() {
  make -C $pkgname-$_basever check
}

package() {
  make -C $pkgname-$_basever DESTDIR="$pkgdir" install
  #ln -s bash "$pkgdir"/opt/arch/bin/sh
}

md5sums=('148888a7c95ac23705559b6f477dfe25'
         'SKIP'
         '817d01a6c0af6f79308a8b7b649e53d8'
         'SKIP'
         '765e14cff12c7284009772e8e24f2fe0'
         'SKIP'
         '49e7da93bf07f510a2eb6bb43ac3e5a2'
         'SKIP'
         '4557d674ab5831a5fa98052ab19edaf4'
         'SKIP'
         'cce96dd77cdd1d293beec10848f6cbb5'
         'SKIP'
         'd3379f8d8abce5c6ee338f931ad008d5'
         'SKIP'
         'ec38c76ca439ca7f9c178e9baede84fc'
         'SKIP'
         'e0ba18c1e3b94f905da9b5bf9d38b58b'
         'SKIP'
         'e952d4f44e612048930c559d90eb99bb'
         'SKIP'
         '57b5b35955d68f9a09dbef6b86d2c782'
         'SKIP'
         'cc896e1fa696b93ded568e557e2392d5'
         'SKIP'
         'fa47fbfa56fb7e9e5367f19a9df5fc9e'
         'SKIP'
)
