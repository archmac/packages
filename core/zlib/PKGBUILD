pkgbase=zlib
pkgname=(zlib minizip)
epoch=1
pkgver=1.2.11
pkgrel=3
arch=('x86_64')
license=('custom')
url="https://www.zlib.net/"
depends=('libc')
options=('staticlibs')  # needed by binutils testsuite
source=("https://zlib.net/zlib-${pkgver}.tar.gz"{,.asc})
md5sums=('1c9f62f0778697a09d36121ead88e08e'
         'SKIP')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

prepare() {
	cd ${srcdir}/zlib-$pkgver
	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
	cd ${srcdir}/zlib-$pkgver
	./configure --prefix=/opt/arch
	make

	cd contrib/minizip
	cp Makefile Makefile.orig
	cp ../README.contrib readme.txt
	autoreconf --install
	./configure --prefix=/opt/arch --enable-static=no
	make
}

check() {
	cd ${srcdir}/zlib-$pkgver
	make test

	cd contrib/minizip
	make -f Makefile.orig test
}

package_zlib() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir}

    install_name_tool -id /opt/arch/lib/libz.so.1.2.11 $pkgdir/opt/arch/lib/libz.so.1.2.11

	ginstall -d -m755 ${pkgdir}/opt/arch/share/licenses/zlib
	ginstall -m644 LICENSE ${pkgdir}/opt/arch/share/licenses/zlib/LICENSE
}

package_minizip() {
	pkgdesc='Mini zip and unzip based on zlib'
	depends=('zlib')

	cd ${srcdir}/zlib-$pkgver/contrib/minizip
	make install DESTDIR=${pkgdir}
	ginstall -d -m755 ${pkgdir}/opt/arch/share/licenses/minizip
	ginstall -m644 ${srcdir}/zlib-$pkgver/LICENSE ${pkgdir}/opt/arch/share/licenses/minizip/LICENSE

	# https://github.com/madler/zlib/pull/229
	rm ${pkgdir}/opt/arch/include/minizip/crypt.h
}
