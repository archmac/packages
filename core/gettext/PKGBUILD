pkgname=gettext
pkgver=0.19.8.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('x86_64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
groups=('base' 'base-devel')
depends=()
optdepends=('git: for autopoint infrastructure updates')
options=(!docs)
install=gettext.install
source=(https://ftp.gnu.org/gnu/gettext/${pkgname}-${pkgver}.tar.xz{,.sig})
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871') # Daiki Ueno

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/opt/arch --disable-dependency-tracking --disable-silent-rules --disable-debug --with-included-gettext --with-included-glib --with-included-libcroco --with-included-libunistring --without-git --without-cvs --without-xz
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cp -v /opt/arch/lib/libintl.9.dylib "${pkgdir}/opt/arch/lib"
}
sha256sums=(
  '105556dbc5c3fbbc2aa0edb46d22d055748b6f5c7cd7a8d99f8e7eb84e938be4'
  'SKIP'
)
