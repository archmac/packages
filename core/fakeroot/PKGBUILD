pkgname=fakeroot
pkgver=1.21
pkgrel=1
pkgdesc='Tool for simulating superuser privileges'
arch=('x86_64')
license=('GPL')
url="http://packages.debian.org/fakeroot"
groups=('base-devel')
depends=()
source=(https://mirrors.ocf.berkeley.edu/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.gz
        0002-OS-X-10.10-introduced-id_t-int-in-gs-etpriority.patch
        0001-Implement-openat-2-wrapper-which-handles-optional-ar.patch
        fakeroot-darwin-openat-wraptmp.patch
        fakeroot-darwin-openat.patch)
md5sums=('be5c9a0e516869fca4a6758105968e5a'
         '303823f943f46ba48d11e263641f1edb'
         '8a0db3599d0300f0860ea33bb5cd09df'
         '53b5443661c662b2e4ddf8cef1cf089e'
         '25ece68bb4e16f53b9b0a15e225f63de')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../0002-OS-X-10.10-introduced-id_t-int-in-gs-etpriority.patch
  patch -p1 -i ../0001-Implement-openat-2-wrapper-which-handles-optional-ar.patch
  patch -p1 -i ../fakeroot-darwin-openat.patch
}

build() {
  cd $pkgname-$pkgver
  
  export LIBTOOLIZE=glibtoolize
  ./bootstrap
  ./configure --prefix=/opt/arch \
    --libdir=/opt/arch/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv \
    --disable-dependency-tracking \
    --disable-silent-rules

  make wraptmpf.h
  patch -p1 -i ../fakeroot-darwin-openat-wraptmp.patch

  make
}

package() {
  cd $pkgname-$pkgver
  sed -i~ 's/SUBDIRS = .*/SUBDIRS =/' doc/Makefile # don't depend on po4a
  make DESTDIR="$pkgdir" install

  # install README for sysv/tcp usage
  install -d -m755 "$pkgdir"/opt/arch/share/doc/$pkgname
  install -m644 README "$pkgdir"/opt/arch/share/doc/$pkgname/README
}
