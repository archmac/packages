pkgname=openssl
_ver=1.0.2n
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=()
optdepends=('ca-certificates')
options=('!makeflags')
backup=('opt/arch/etc/ssl/openssl.cnf')
install=openssl.install
source=(
  "https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz"{,.asc}
  'no-rpath.patch'
  'ssl3-test-failure.patch'
  'ca-dir.patch'
  'openssl.install'
  'c_keychain'
)
sha256sums=(
  '370babb75f278c39e0c50e8c4e7493bc0f18db6867478341a832a982fd15a8fe'
  'SKIP'
  '754d6107a306311e15a1db6a1cc031b81691c8b9865e8809ac60ca6f184c957c'
  'c54ae87c602eaa1530a336ab7c6e22e12898e1941012349c153e52553df64a13'
  'bd1dd077ef6d4d8fde1c19064a8c84d7d196e311738fb45f6a3a18d471371b4b'
  '24c038cbdeb04536464a58ecc76abd16b46493c47ee35341dc56a83b8aba7dbb'
  '796d9145ff31b4c678f382ac9f5a4fc982f1b605f90e9f0419482ce8a93187aa'
)
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491')

prepare() {
    cd $srcdir/$pkgname-$_ver

    # remove rpath: http://bugs.archlinux.org/task/14367
    patch -p0 -i $srcdir/no-rpath.patch

    # disable a test that fails when ssl3 is disabled
    patch -p1 -i $srcdir/ssl3-test-failure.patch

    # set ca dir to /etc/ssl by default
    patch -p0 -i $srcdir/ca-dir.patch
}

build() {
    cd $srcdir/$pkgname-$_ver

    perl ./Configure --prefix=/opt/arch --openssldir=/opt/arch/etc/ssl no-ssl2 zlib-dynamic shared enable-cms darwin64-x86_64-cc enable-ec_nistp_64_gcc_128
    #./Configure --prefix=/opt/arch --openssldir=/opt/arch/etc/ssl --libdir=/opt/arch/lib \
    #    shared no-ssl3-method ${optflags} \
    #    "${openssltarget}" \
    #    "-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

    make depend
    make
}

check() {
    cd $srcdir/$pkgname-$_ver
    # the test fails due to missing write permissions in /etc/ssl
    # revert this patch for make test
    patch -p0 -R -i $srcdir/ca-dir.patch
    make test
    patch -p0 -i $srcdir/ca-dir.patch
}

package() {
    cd $srcdir/$pkgname-$_ver
    make INSTALL_PREFIX=$pkgdir MANDIR=/opt/arch/share/man MANSUFFIX=ssl install
    ginstall -m755 $srcdir/c_keychain $pkgdir/opt/arch/bin/c_keychain
    ginstall -d -m755 $pkgdir/opt/arch/share/licenses/$pkgname
    ginstall -m644 LICENSE $pkgdir/opt/arch/share/licenses/$pkgname/LICENSE
}
