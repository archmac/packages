pkgname=gpgme
pkgver=1.10.0
pkgrel=2
pkgdesc="A C wrapper library for GnuPG"
arch=('x86_64')
url="http://www.gnupg.org/related_software/gpgme/"
license=('LGPL')
depends=('libgpg-error' 'gnupg>=2')
install=${pkgname}.install
options=('!emptydirs')
source=(https://www.gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=(
  '1a8fed1197c3b99c35f403066bb344a26224d292afc048cfdfc4ccd5690a0693'
  'SKIP'
)
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/opt/arch --disable-fd-passing --disable-static \
              --disable-gpgsm-test
  sed -i .orig -e 's/DIST_SUBDIRS = src tests doc lang/DIST_SUBDIRS = src doc lang/' Makefile
  sed -i .orig -e 's/SUBDIRS = src ${tests} doc lang/SUBDIRS = src doc lang/' Makefile
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
