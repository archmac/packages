pkgname=libgpg-error
pkgver=1.27
pkgrel=2
pkgdesc="Support library for libgcrypt"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=()
source=(https://www.gnupg.org/ftp/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2{,.sig})
install=libgpg-error.install
# https://www.gnupg.org/download/integrity_check.html
sha256sums=(
  '4f93aac6fecb7da2b92871bb9ee33032be6a87b174f54abf8ddf0911a22d29d2'
  'SKIP'
)
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/opt/arch
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
}
