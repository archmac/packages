pkgname=pacman
pkgver=5.0.2
pkgrel=2
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
#depends=('bash' 'glibc' 'libarchive' 'curl'
#         'gpgme' 'pacman-mirrorlist' 'archlinux-keyring')
depends=('bash' 'libarchive' 'gnupg' 'pacman-mirrorlist')
makedepends=('asciidoc')   # roundup patch alters docs
checkdepends=('python2' 'fakechroot')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
backup=(opt/arch/etc/pacman.conf opt/arch/etc/makepkg.conf)
#options=('strip' 'debug')
options=('strip')
source=(
  https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz{,.sig}
  pacman-usr.patch
  pacman-usr-local.patch
  pacman-tmp-scriptlet.patch
  pacman-lazy-chroot.patch
  pacman.conf.x86_64
  makepkg.conf
)
sha256sums=(
  'dfd36086ad68564bcd977f4a1fafe51dd328acd4a95093ac4bf1249be9c41f0e'
  'SKIP'
  '689d02182ca2aef4adfc6efa9d0fcecc0402a31fea461e4c2a57bc725bb97fdd'
  'dcf9280a74af19ce84adee2a691b37b79ee7b2543968eccb51df411f6babce9c'
  '8fd668217dac6c1086b8658de0534a5ca080a97ed684c12519df666aeb653e7a'
  '261468877f7b7cdfa597c34f66ea8f12adde6d37e76b8c5019f13b4fe82c6f3d'
  '42c190510e576734365ed55271ddcf616845555f406c3d7a03c8a548ac9af12e'
  '3b380092065991eea4d1f95affee93c88ca8cf55315c4f596a1ff43d4ba165de'
)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD')  # Allan McRae <allan@archlinux.org>


prepare() {
  cd "$pkgname-$pkgver"

  patch -p0 < ../pacman-usr.patch
  patch -p0 < ../pacman-usr-local.patch
  patch -p0 < ../pacman-tmp-scriptlet.patch
  patch -p0 < ../pacman-lazy-chroot.patch
}

build() {
  cd "$pkgname-$pkgver"

  export LIBARCHIVE_CFLAGS="-I/opt/arch/include"
  export LIBARCHIVE_LIBS="-larchive"
  export LIBCURL_CFLAGS="-I/usr/include/curl"
  export LIBCURL_LIBS="-lcurl"
  #export LIBSSL_CFLAGS="-I${bootstrap_dir/include"
  #export LIBSSL_LIBS="-lssl"

  ./configure --prefix=/opt/arch --sysconfdir=/opt/arch/etc \
    --localstatedir=/opt/arch/var --enable-doc \
    --with-scriptlet-shell=/opt/arch/bin/bash \
    --with-curl \
    --disable-silent-rules \
    --disable-dependency-tracking
    #--with-ldconfig=/usr/bin/ldconfig
  make V=1
  make -C contrib
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C contrib install

  # install Arch specific stuff
  install -d -m755 "$pkgdir/opt/arch/etc"
  install -m644 "$srcdir/pacman.conf.$CARCH" "$pkgdir/opt/arch/etc/pacman.conf"

  case $CARCH in
    x86_64)
      mycarch="x86_64"
      mychost="x86_64-apple-darwin15.4.0"
      myflags="-march=x86-64"
      ;;
  esac

  # set things correctly in the default conf file
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/opt/arch/etc"
  sed -i~ \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g" \
    "$pkgdir/opt/arch/etc/makepkg.conf"

  ## put bash_completion in the right location
  install -d m755 "$pkgdir/opt/arch/share/bash-completion/completions"
  mv "$pkgdir/opt/arch/etc/bash_completion.d/pacman" "$pkgdir/opt/arch/share/bash-completion/completions"
  rmdir "$pkgdir/opt/arch/etc/bash_completion.d"

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/opt/arch/share/bash-completion/completions/$f"
  done

  install -d -m755 "$pkgdir/opt/arch/share/vim/vimfiles/syntax"
  install -m644 contrib/PKGBUILD.vim "$pkgdir/opt/arch/share/vim/vimfiles/syntax/PKGBUILD.vim"
}
