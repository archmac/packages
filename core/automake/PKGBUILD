pkgname=automake
pkgver=1.15
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=()
makedepends=('autoconf')
checkdepends=('dejagnu' 'python' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope')
install=automake.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('9a1ddb0e053474d9d1105cfe39b0c48d'
         'SKIP')
validpgpkeys=('E1622F96D2BB4E58018EEF9860F906016E407573')   # Stefano Lattarini

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # skip failing test...
  sed -i~ -e "/instmany-python.sh/d" Makefile.in
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/opt/arch
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  (perl -pe 's/^ {4}//' > "$pkgdir/opt/arch/share/aclocal/dirlist") <<<"
    /opt/arch/share/aclocal
    /usr/share/aclocal
  "
}
