pkgname=libarchive
pkgver=3.1.2
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=('x86_64')
url="http://libarchive.org/"
license=('BSD')
depends=()
#options=('strip' 'debug' 'libtool')
options=('strip' 'libtool')
#provides=('libarchive.so')
source=("http://libarchive.org/downloads/$pkgname-$pkgver.tar.gz"
        '0001-mtree-fix-line-filename-length-calculation.patch'
        '0001-Limit-write-requests-to-at-most-INT_MAX.patch'
        'libarchive-3.1.2-acl.patch'
        'libarchive-3.1.2-sparce-mtree.patch')
md5sums=('efad5a503f66329bb9d2f4308b5de98a'
         'fda89c145bbcd793a96b06b463ef6a72'
         '9bf80940bd3ce861137a0a8dcacf5705'
         'a5c995661c62429ceff2c23ea322393b'
         'cb344a879b3c4550fe3faf86c3826f23')

prepare() {
  cd "$pkgname-$pkgver"

  # https://code.google.com/p/libarchive/issues/detail?id=301
  # upstream commit e65bf287f0133426b26611fe3e80b51267987106
  patch -Np1 -i "$srcdir/0001-mtree-fix-line-filename-length-calculation.patch"

  # https://code.google.com/p/libarchive/issues/detail?id=329
  patch -Np1 -i "$srcdir/libarchive-3.1.2-acl.patch"

  # CVE-2013-0211
  patch -Np1 -i "$srcdir/0001-Limit-write-requests-to-at-most-INT_MAX.patch"

  # upstream commit 977bf2a4 - improved mtree support
  patch -p1 -i $srcdir/libarchive-3.1.2-sparce-mtree.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./build/autogen.sh
  ./configure --prefix=/opt/arch --without-xml2 --without-nettle --without-lzo2
  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -d -m755 "$pkgdir"/opt/arch/share/licenses/libarchive
  install -m644 COPYING "$pkgdir"/opt/arch/share/licenses/libarchive/COPYING
}
