pkgname=crystal
pkgver=0.23.0
pkgrel=1
_binary_rel=1
pkgdesc='The Crystal Programming Language'
arch=(i686 x86_64)
url='http://crystal-lang.org'
license=(Apache)
#depends=(gc libatomic_ops pcre libevent llvm-libs)
depends=(gc libatomic_ops pcre libevent)
#makedepends=(libxml2 llvm)
makedepends=(libxml2)
checkdepends=(libyaml libxml2 gmp inetutils git)
optdepends=('shards: crystal language package manager'
            'libyaml: For YAML support'
            'gmp: For BigInt support'
            'libxml2: For XML support')
source=(crystal-$pkgver.tar.gz::https://github.com/crystal-lang/crystal/archive/$pkgver.tar.gz)
source_i686+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-i686.tar.gz)
source_x86_64+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-x86_64.tar.gz)
sha256sums=('9b65904bb55100994a3b8022b9c553e5aa78979f459c8b10aa64053a65e5d517')
sha256sums_i686=('0a758770e6cb277bc5ed38fb6edafbf820b99527b65d6a44fd9f3a4c4743b70f')
sha256sums_x86_64=('1d0348e1df3c56f9c29a3a76254c074846389fe5bed659c2cc1051bab83e1a5a')

prepare() {
  cd $pkgname-$pkgver
  sed -i~ -e 's|Please install shards: https://github.com/ysbaddaden/shards|Please install shards: pacman -S shards|' src/compiler/crystal/command.cr
}

build() {
  cd $pkgname-$pkgver

  # We use --no-debug because it runs out of memory on i686 without it
  # https://github.com/crystal-lang/crystal/issues/3787#issuecomment-281591985
  make release=1 \
       FLAGS="--release --no-debug" \
       PATH="$srcdir/$pkgname-$pkgver-$_binary_rel/bin:$PATH" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="lib:/opt/arch/lib/crystal" \
       CRYSTAL_CACHE_DIR="/tmp/crystal"
  make doc CRYSTAL_CACHE_DIR="/tmp/crystal"
}

check() {
  cd $pkgname-$pkgver

  # disable tests as it fails with OOM at i686
  true || make spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver" \
            CRYSTAL_CACHE_DIR="/tmp/crystal" \
            PATH=".build:$PATH"
}

package() {
  cd $pkgname-$pkgver

  # /opt/arch/bin/crystal                compiled executable
  # /opt/arch/lib/crystal/               compiler src & core libs
  # /opt/arch/share/doc/crystal/api      api docs
  # /opt/arch/share/doc/crystal/samples/ samples

  install -dm755 "$pkgdir/opt/arch/bin"
  install -m755 ".build/crystal" "$pkgdir/opt/arch/bin/crystal"

  install -dm755 "$pkgdir/opt/arch/lib"
  cp -r src "$pkgdir/opt/arch/lib/crystal"

  install -dm755 "$pkgdir/opt/arch/share/doc/crystal"
  cp -r doc "$pkgdir/opt/arch/share/doc/crystal/api"
  cp -r samples "$pkgdir/opt/arch/share/doc/crystal/"

  install -dm755 "$pkgdir/opt/arch/share/bash-completion/completions"
  install -m644 etc/completion.bash "$pkgdir/opt/arch/share/bash-completion/completions/crystal"
  install -dm755 "$pkgdir/opt/arch/share/zsh/site-functions"
  install -m644 etc/completion.zsh "$pkgdir/opt/arch/share/zsh/site-functions/_crystal"

  install -Dm644 LICENSE "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"
}
