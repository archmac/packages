pkgname=redis
pkgver=3.0.7
pkgrel=1
pkgdesc='Advanced key-value store'
arch=('x86_64')
url='http://redis.io/'
license=('BSD')
#depends=('jemalloc' 'grep' 'shadow')
depends=('jemalloc' 'grep')
backup=('opt/arch/etc/redis.conf')
install=redis.install
source=(http://download.redis.io/releases/redis-$pkgver.tar.gz
        redis.conf-sane-defaults.patch
        redis-2.8.11-use-system-jemalloc.patch
        org.arch-osx.redis.plist
        )
md5sums=('84ed3f486e7a6f0ebada6917370f3532'
         '051f072d6fe378b29799a0333d979f1d'
         '4a10f51be7e7eac5160044c57361e7c3'
         '3dd49fda5e65f2e13bb7a40a97c353f3')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/redis.conf-sane-defaults.patch
  patch -p1 -i "$srcdir"/redis-2.8.11-use-system-jemalloc.patch
}

build() {
  make -C $pkgname-$pkgver
}

# pops up lots of prompts
#check() {
#  make -C $pkgname-$pkgver test
#}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir"/opt/arch install

  install -dm755 "$pkgdir"/opt/arch/share/licenses/redis
  install -m644 COPYING "$pkgdir"/opt/arch/share/licenses/redis/LICENSE
  install -dm755 "$pkgdir"/opt/arch/etc
  install -m644 redis.conf "$pkgdir"/opt/arch/etc/redis.conf

  ln -sf redis-server $pkgdir/opt/arch/bin/redis-sentinel

  mkdir -p "$pkgdir"/Library/LaunchDaemons
  install -m644 "$srcdir"/org.arch-osx.redis.plist "$pkgdir"/Library/LaunchDaemons
}
