pkgname=htop
pkgver=2.0.2
pkgrel=3
pkgdesc="Interactive process viewer"
arch=('x86_64')
url="http://hisham.hm/htop/"
license=('GPL')
depends=('ncurses')
makedepends=('python3')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
options=('!emptydirs')
validpgpkeys=('8460980B2B79786DE0C7FCC83FD8F43C2BB3C478') # Hisham Muhammad <h@hisham.hm>
source=("https://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
'https://github.com/hishamhm/htop/commit/b2771218.patch?full_index=1')
sha256sums=('179be9dccb80cee0c5e1a1f58c8f72ce7b2328ede30fb71dcdf336539be2f487'
            'SKIP'
            '3369f8aed21706d809db062f25fd46bf9c0677712a624697bc5415aa45d5d104')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < "${srcdir}/b2771218.patch?full_index=1"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/opt/arch \
      --sysconfdir=/opt/arch/etc
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
