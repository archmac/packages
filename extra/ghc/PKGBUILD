pkgname=ghc
pkgver=8.0.1
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
#depends=('perl' 'gmp' 'gcc' 'libffi')
#makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl')
provides=('haskell-array=0.5.1.1'
          'haskell-base=4.9.0.0'
          'haskell-binary=0.8.3.0'
          'haskell-bytestring=0.10.8.1'
          'haskell-containers=0.5.7.1'
          'haskell-deepseq=1.4.2.0'
          'haskell-directory=1.2.6.2'
          'haskell-filepath=1.4.1.0'
          'haskell-ghc-boot=8.0.1'
          'haskell-ghc-boot-th=8.0.1'
          'haskell-ghc-prim=0.5.0.0'
          'haskell-haskeline=0.7.2.3'
          'haskell-hoopl=3.10.2.1'
          'haskell-hpc=0.6.0.3'
          'haskell-integer-gmp=1.0.0.1'
          'haskell-pretty=1.1.3.3'
          'haskell-process=1.4.2.0'
          'haskell-template-haskell=2.11.0.0'
          'haskell-terminfo=0.4.0.2'
          'haskell-time=1.6.0.1'
          'haskell-transformers=0.5.2.0'
          'haskell-unix=2.7.2.0'
          'haskell-xhtml=3000.2.1'
          'haskell-cabal=1.24.0.0')
install='ghc.install'
source=("https://downloads.haskell.org/~ghc/$pkgver/$pkgname-${pkgver}-src.tar.xz"
        ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook
        "https://downloads.haskell.org/~ghc/$pkgver/ghc-${pkgver}-x86_64-apple-darwin.tar.xz"
        http://ftpmirror.gnu.org/gmp/gmp-6.1.0.tar.bz2
        b7b130c5102948b38aaba723044288e16a80d492.diff)
noextract=("$pkgname-${pkgver}-src.tar.xz" "$pkgname-${pkgver}-x86_64-apple-darwin.tar.xz")
sha256sums=('90fb20cd8712e3c0fbeb2eac8dab6894404c21569746655b9b12ca9684c7d1d2'
            'e9da27df805378dc7bde8d45c58b720c33d27e4ca3af944daf3a60a1942d266a'
            '4c639686acf98d657ff002fea82d95cafb04b29538b144546006797bede19c36'
            '35b07eb3e773d7b90827496f1e4fef3b1e212fd9fa1ed633201327278fec7650'
            '06ec33056b927da5e68055147f165f873088f6812fe0c642c4c78c9a449fbc42'
            '498449a994efeba527885c10405993427995d3f86b8768d8cdf8d9dd7c6b73e8'
            'c0ee7af71e6c02ac1f8e82ccfc7054349ad732a2005dcf16f9a88e9565b9371a')

prepare() {
  cd ${srcdir}

  mkdir -p bootstrap

  bsdtar xf "$pkgname-${pkgver}-x86_64-apple-darwin.tar.xz" -C bootstrap
  cd bootstrap/ghc-$pkgver
  ./configure --prefix=${srcdir}/bootstrap/binary
  make -j1 install
  cd -

  # Need to extract this tarball with a UTF-8 locale instead of a chroot's "C"
  # locale; otherwise we get:
  #   bsdtar: Pathname can't be converted from UTF-8 to current locale.
  LANG=en_US.UTF-8 bsdtar xf $pkgname-${pkgver}-src.tar.xz

  cd ghc-$pkgver

  patch -p1 -i ../b7b130c5102948b38aaba723044288e16a80d492.diff

  # We select the following build profile:
  #   Full build with max optimisation and everything enabled (very slow build)
  #cp mk/build.mk{.sample,}
  #sed -i '1iBuildFlavour = perf' mk/build.mk
}

build() {
  cd gmp-6.1.0

  ./configure --prefix=${srcdir}/integer-gmp --with-pic --disable-shared
  make
  make check
  make -j1 install

  cd -

  cd ghc-$pkgver

  export PATH="${srcdir}/bootstrap/binary/bin:$PATH"

  ./configure \
    --prefix=/opt/arch \
    --docdir=/opt/arch/share/doc/ghc \
    --with-gmp-includes=${srcdir}/integer-gmp/include \
    --with-gmp-libraries=${srcdir}/integer-gmp/lib \
    --with-nm=$(xcrun --find nm-classic)
  make
}

package() {
  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  install -d -m755 \
    "$pkgdir/opt/arch/share/bash-completion/completions"
  install -m644 utils/completion/ghc.bash \
    "$pkgdir/opt/arch/share/bash-completion/completions/ghc"

  install -d -m755 \
    "$pkgdir/opt/arch/share/libalpm/hooks/"
  for _hook in ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook; do
    install -m644 "$srcdir/$_hook" \
      "$pkgdir/opt/arch/share/libalpm/hooks/$_hook"
  done

  install -d -m755 "${pkgdir}"/opt/arch/share/haskell/{register,unregister}

  install -d -m755 "$pkgdir/opt/arch/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"
}
