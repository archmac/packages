pkgbase=lpeg
pkgname=(lua-lpeg lua51-lpeg lua52-lpeg)
pkgver=1.0.1
pkgrel=2
arch=('x86_64')
url='http://www.inf.puc-rio.br/~roberto/lpeg'
license=('MIT')
makedepends=('lua' 'lua51' 'lua52')
source=(http://www.inf.puc-rio.br/~roberto/$pkgbase/$pkgbase-$pkgver.tar.gz
        LICENSE)
sha256sums=('62d9f7a9ea3c1f215c77e0cadd8534c6ad9af0fb711c3f89188a8891c72f026b'
            '6be7688c9c648e7c7cde693081be938c869a10aa4de09fe0a4c4c1d66b3cf03d')

prepare() {
  sed -i~ -e 's/^CFLAGS =/CFLAGS +=/' $pkgbase-$pkgver/makefile
  sed -i~ -e 's/^COPT =/# COPT =/' $pkgbase-$pkgver/makefile

  cp -r $pkgbase-$pkgver $pkgbase-$pkgver-51
  cp -r $pkgbase-$pkgver $pkgbase-$pkgver-52
}

build() {
  cd $pkgbase-$pkgver
  make LUADIR=/opt/arch/include

  cd "$srcdir"/$pkgbase-$pkgver-51
  make LUADIR=/opt/arch/include/lua5.1

  cd "$srcdir"/$pkgbase-$pkgver-52
  make LUADIR=/opt/arch/include/lua5.2
}

check() {
  msg2 "Testing with Lua 5.3"
  cd $srcdir/$pkgbase-$pkgver
  lua test.lua

  msg2 "Testing with Lua 5.1"
  cd $srcdir/$pkgbase-$pkgver-51
  lua5.1 test.lua

  msg2 "Testing with Lua 5.2"
  cd $srcdir/$pkgbase-$pkgver-52
  lua5.2 test.lua
}

package_lua-lpeg() {
  pkgdesc='Pattern-matching library for Lua 5.3'
  depends=('lua')

  cd $pkgbase-$pkgver
  install -m755 -d "$pkgdir"/opt/arch/lib/lua/5.3
  install -m755 $pkgbase.dylib "$pkgdir"/opt/arch/lib/lua/5.3/$pkgbase.dylib
  install -m755 -d "$pkgdir"/opt/arch/share/lua/5.3
  install -m644 re.lua "$pkgdir"/opt/arch/share/lua/5.3/re.lua
  install -m755 -d "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 ../LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}

package_lua51-lpeg() {
  pkgdesc='Pattern-matching library for Lua 5.1'
  depends=('lua51')

  cd $pkgbase-$pkgver-51
  install -m755 -d "$pkgdir"/opt/arch/lib/lua/5.1
  install -m755 $pkgbase.dylib "$pkgdir"/opt/arch/lib/lua/5.1/$pkgbase.dylib
  install -m755 -d "$pkgdir"/opt/arch/share/lua/5.1
  install -m644 re.lua "$pkgdir"/opt/arch/share/lua/5.1/re.lua
  install -m755 -d "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 ../LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}

package_lua52-lpeg() {
  pkgdesc='Pattern-matching library for Lua 5.2'
  depends=('lua52')

  cd $pkgbase-$pkgver-52
  install -m755 -d "$pkgdir"/opt/arch/lib/lua/5.2
  install -m755 $pkgbase.dylib "$pkgdir"/opt/arch/lib/lua/5.2/$pkgbase.dylib
  install -m755 -d "$pkgdir"/opt/arch/share/lua/5.2
  install -m644 re.lua "$pkgdir"/opt/arch/share/lua/5.2/re.lua
  install -m755 -d "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 ../LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}
