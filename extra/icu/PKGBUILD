pkgname=icu
pkgver=61.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=(x86_64)
url="http://www.icu-project.org/"
license=('custom:icu')
#depends=('gcc-libs>=4.7.1-5' 'sh')
depends=('sh')
#makedepends=('clang')
source=(http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz{,.asc})
md5sums=(
  '68fe38999fef94d622bd6843d43c0615'
  'SKIP'
)
validpgpkeys=(
  'BA90283A60D67BA0DD910A893932080F4FB419E3' #  "Steven R. Loomis (filfla-signing) <srloomis@us.ibm.com>"
  '9731166CD8E23A83BEE7C6D3ACA5DBE1FD8FABF1' #  "Steven R. Loomis (ICU Project) <srl@icu-project.org>"
  'FFA9129A180D765B7A5BEA1C9B432B27D1BA20D7' # "Fredrik Roubert <fredrik@roubert.name>"
)

build() {
  cd icu/source
  ./configure --prefix=/opt/arch \
              --sysconfdir=/opt/arch/etc \
              --mandir=/opt/arch/share/man \
              --sbindir=/opt/arch/bin
  make
}

check() {
  cd icu/source
  make -k check # passes all
}

package() {
  cd icu/source
  make -j1 DESTDIR=${pkgdir} install

  # Install license
  install -d -m755 ${pkgdir}/opt/arch/share/licenses/icu
  install -m644 ${srcdir}/icu/license.html ${pkgdir}/opt/arch/share/licenses/icu/license.html
}
