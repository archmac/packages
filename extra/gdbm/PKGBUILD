pkgname=gdbm
pkgver=1.11
pkgrel=1
pkgdesc="GNU database library"
url="http://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL3')
arch=('x86_64')
depends=('libc' 'sh')
options=('!makeflags')
install=gdbm.install
source=(ftp://ftp.gnu.org/gnu/gdbm/${pkgname}-${pkgver}.tar.gz{,.sig}
        gdbm-1.10-zeroheaders.patch)
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732')
md5sums=('72c832680cf0999caedbe5b265c8c1bd'
         'SKIP'
         'ac255b10452005237836cd2d3a470733')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Prevent gdbm from storing uninitialized memory content
  # to database files. This patch improves security, as the
  # uninitialized memory might contain sensitive informations
  # from other applications.
  # https://bugzilla.redhat.com/show_bug.cgi?id=4457
  # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=208927
  patch -Np1 -i ../gdbm-1.10-zeroheaders.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/opt/arch --enable-libgdbm-compat
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # create symlinks for compatibility
  install -dm755 "${pkgdir}"/opt/arch/include/gdbm
  ln -sf ../gdbm.h "${pkgdir}"/opt/arch/include/gdbm/gdbm.h
  ln -sf ../ndbm.h "${pkgdir}"/opt/arch/include/gdbm/ndbm.h
  ln -sf ../dbm.h  "${pkgdir}"/opt/arch/include/gdbm/dbm.h
}
