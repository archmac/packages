pkgname=yasm
pkgver=1.3.0
pkgrel=2
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.)"
arch=('x86_64')
url="http://www.tortall.net/projects/yasm/"
license=('BSD' 'GPL2' 'LGPL2.1' 'PerlArtistic')
options=('staticlibs')
source=(http://www.tortall.net/projects/yasm/releases/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3dce6601b495f5b3d45b59f7d2492a340ee7e84b5beca17e48f862502bd5603f')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/opt/arch
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/opt/arch/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/opt/arch/share/licenses/${pkgname}/LICENSE"
  install -m644 BSD.txt "${pkgdir}/opt/arch/share/licenses/${pkgname}/BSD.txt"
}
