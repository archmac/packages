pkgbase=vim
pkgname=(vim vim-runtime)
pkgver=7.4.1689
_versiondir=74
pkgrel=1
arch=(x86_64)
license=('custom:vim')
url='http://www.vim.org'
#makedepends=(gpm python2 python ruby libxt desktop-file-utils gtk2 gtk3 lua gawk tcl)
makedepends=()
source=(vim-$pkgver.tar.gz::http://github.com/vim/vim/archive/v$pkgver.tar.gz)
sha1sums=('9fdd5cadca97baabc475b154f249b4b8d03fff07')

prepare() {
  cd vim-$pkgver/src

  # define the place for the global (g)vimrc file (set to /opt/arch/etc/vimrc)
  sed -i~ -e 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' feature.h
  sed -i~ -e 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' feature.h

  autoconf

  cd "$srcdir"
  cp -a vim-$pkgver gvim-$pkgver
}

build() {
  cd "${srcdir}"/vim-$pkgver
  ./configure \
    --prefix=/opt/arch \
    --localstatedir=/opt/arch/var/lib/vim \
    --with-features=huge \
    --with-compiledby='Arch OS X' \
    --enable-acl \
    --with-x=no \
    --disable-gui \
    --enable-multibyte \
    --enable-cscope \
    --enable-netbeans \
    --enable-perlinterp=dynamic \
    --enable-pythoninterp=dynamic \
    --enable-rubyinterp=dynamic \
    #--enable-python3interp=dynamic \
    #--enable-luainterp=dynamic \
    #--enable-tclinterp=dynamic
  make
}

check() {
  # disable tests because they seem to freeze
  cd "${srcdir}"/vim-$pkgver
  #make test
}

package_vim-runtime() {
  pkgdesc='Runtime for vim'

  cd "${srcdir}"/vim-$pkgver

  make -j1 VIMRCLOC=/opt/arch/etc DESTDIR="${pkgdir}" install
  # man and bin files belong to 'vim'
  rm -r "${pkgdir}"/opt/arch/share/man/ "${pkgdir}"/opt/arch/bin/

  # Don't forget logtalk.dict
  install -d -m755 \
    "${pkgdir}"/opt/arch/share/vim/vim${_versiondir}/ftplugin

  install -m644 runtime/ftplugin/logtalk.dict \
    "${pkgdir}"/opt/arch/share/vim/vim${_versiondir}/ftplugin/logtalk.dict

  # fix FS#17216
  sed -i~ -e 's|messages,/var|messages,/var/log/messages.log,/var|' \
    "${pkgdir}"/opt/arch/share/vim/vim${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i~ -e "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    "${pkgdir}"/opt/arch/share/vim/vim${_versiondir}/filetype.vim
  #sed -i~ -e "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
  #  "${pkgdir}"/opt/arch/share/vim/vim${_versiondir}/ftplugin/changelog.vim

  # rgb.txt file
  install -d -m755 \
    "${pkgdir}"/opt/arch/share/vim/vim${_versiondir}
  install -m644 runtime/rgb.txt \
    "${pkgdir}"/opt/arch/share/vim/vim${_versiondir}/rgb.txt

  # license
  install -dm755 "${pkgdir}"/opt/arch/share/licenses/vim-runtime
  ln -s /opt/arch/share/vim/vim${_versiondir}/doc/uganda.txt \
    "${pkgdir}"/opt/arch/share/licenses/vim-runtime/license.txt
}

package_vim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
  #depends=("vim-runtime=${pkgver}-${pkgrel}" 'gpm' 'acl')
  depends=("vim-runtime=${pkgver}-${pkgrel}")
  optdepends=('python2: Python 2 language support'
              'python: Python 3 language support'
              'ruby: Ruby language support'
              'lua: Lua language support'
              'perl: Perl language support'
              'tcl: Tcl language support')
  conflicts=('gvim' 'vim-minimal' 'vim-python3')
  provides=('xxd' 'vim-minimal' 'vim-python3')
  replaces=('vim-python3' 'vim-minimal')

  cd "${srcdir}"/vim-$pkgver
  make -j1 VIMRCLOC=/opt/arch/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}"/opt/arch/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/opt/arch/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
  rm -r "${pkgdir}"/opt/arch/share/vim

  # license
  install -d -m755 \
    "${pkgdir}"/opt/arch/share/licenses/${pkgname}
  install -m644 runtime/doc/uganda.txt \
    "${pkgdir}"/opt/arch/share/licenses/${pkgname}/license.txt
}
