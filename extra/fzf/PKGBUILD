pkgname=fzf
pkgver=0.17.1
_pkgver=0.17.1
pkgrel=1
pkgdesc="Command-line fuzzy finder"
arch=("x86_64")
url="https://github.com/junegunn/fzf"
license=("MIT")
makedepends=("git" "go")
optdepends=("fish: fish keybindings"
	"tmux: fzf-tmux script for launching fzf in a tmux pane"
	"vim: plugin"
	"zsh: zsh keybindings")
source=("$pkgname-$pkgver.tar.gz::https://github.com/junegunn/fzf/archive/$_pkgver.tar.gz"
	"git+https://github.com/junegunn/$pkgname.wiki")
sha256sums=('9c881e55780c0f56b5a30b87df756634d853bfd3938e7e53cb2df6ed63aa84a7'
            'SKIP')

build() {
	cd "$srcdir/$pkgname-$_pkgver"

	msg2 'Building...'
	GOPATH="$srcdir" TMPDIR=/tmp go get -u github.com/junegunn/fzf/...
}

package() {
	cd "$srcdir/$pkgname-$_pkgver"

	## Doc
	install -dm755 "$pkgdir/opt/arch/share/doc/fzf/wiki"
	install -m644 "$srcdir/fzf.wiki"/* "$pkgdir/opt/arch/share/doc/fzf/wiki"
	install -m644 README.md install uninstall "$pkgdir/opt/arch/share/doc/fzf"

	## Man page
	install -dm755 "$pkgdir/opt/arch/share/man/man1"
	install -m644 "man/man1/fzf.1" "$pkgdir/opt/arch/share/man/man1/fzf.1"

	## License
	install -dm755 "$pkgdir/opt/arch/share/licenses/fzf"
	install -m644 "LICENSE" "$pkgdir/opt/arch/share/licenses/fzf/LICENSE"

	## Binaries
	install -dm755 "$pkgdir/opt/arch/bin"
	install -m755 "$srcdir/bin/fzf" "bin/fzf-tmux" "$pkgdir/opt/arch/bin/"

	## Completion and keybindings
	install -dm755 "$pkgdir/opt/arch/share/fzf"
	install -m644 shell/*.bash shell/*.zsh "$pkgdir/opt/arch/share/fzf"

	## Fish keybindings
	install -dm755 "$pkgdir/opt/arch/share/fish/functions"
	install -m644 "shell/key-bindings.fish" "$pkgdir/opt/arch/share/fish/functions/fzf_key_bindings.fish"

	## Vim plugin
	install -dm755 "$pkgdir"/opt/arch/share/vim/vimfiles/plugin
	install -m644 plugin/fzf.vim "$pkgdir"/opt/arch/share/vim/vimfiles/plugin/fzf.vim
}
