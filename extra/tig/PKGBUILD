pkgname=tig
pkgver=2.2.1
pkgrel=1
pkgdesc='Text-mode interface for Git.'
depends=('git' 'ncurses')
makedepends=('asciidoc' 'xmlto')
url='http://jonas.nitro.dk/tig/'
license=('GPL')
arch=('i686' 'x86_64')
backup=('etc/tigrc')
source=("https://github.com/jonas/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('c93217874652d5c970e23d188df62a7c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/opt/arch --sysconfdir=/opt/arch/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install install-doc-man

  install -dm0755 "$pkgdir/opt/arch/share/bash-completion/completions"
  install -m0644 contrib/tig-completion.bash "$pkgdir/opt/arch/share/bash-completion/completions/tig"
}
