pkgname=libatomic_ops
pkgver=7.4.6
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('i686' 'x86_64')
url="https://github.com/ivmai/libatomic_ops"
depends=('libc')
license=('GPL2' 'MIT')
source=($url/releases/download/v$pkgver/${pkgname}-${pkgver}.tar.gz)
sha256sums=('96e88ba450ae5fa10aa8e94e6b151a63ffbe47f8069574bd12da22ae80c686db')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/opt/arch --disable-static --enable-shared
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/opt/arch/share/licenses/$pkgname"
  install -m644 doc/LICENSING.txt "$pkgdir/opt/arch/share/licenses/$pkgname/LICENSE"
  rm -rf "$pkgdir/opt/arch/share/doc/"
}
