pkgname=http-parser
pkgver=2.8.1
pkgrel=1
pkgdesc="Parser for HTTP Request/Response written in C"
arch=('x86_64')
url="https://github.com/nodejs/http-parser"
license=('MIT')
depends=('libc')
makedepends=('git')
source=(
  "https://github.com/nodejs/http-parser/archive/v${pkgver}.tar.gz"
  install.patch
)
sha256sums=(
  '51615f68b8d67eadfd2482decc63b3e55d749ce0055502bbb5b0032726d22d96'
  'c0eb1a42fa899203bebafcd6dc8920042345f89264b12eeb2ec85bc65921aca6'
)

prepare() {
  cd $pkgname-$pkgver
  patch -Np2 < ../install.patch
  sed -i -e 's|-Werror||' Makefile
}

build() {
  cd $pkgname-$pkgver
  make library
}

check() {
  cd $pkgname-$pkgver
  make test
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/opt/arch" install

  install -dm755 \
        "${pkgdir}/opt/arch/share/licenses/${pkgname}"
  install -m644 LICENSE-MIT \
        "${pkgdir}/opt/arch/share/licenses/${pkgname}/LICENSE-MIT"
}
