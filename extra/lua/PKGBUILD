pkgname=lua
pkgver=5.3.4
_majorver=${pkgver%.*}
pkgrel=1
pkgdesc='Powerful lightweight programming language designed for extending applications'
arch=('i686' 'x86_64')
url='http://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!emptydirs')
source=(
    https://www.lua.org/ftp/lua-$pkgver.tar.gz
    liblua.so.patch
    lua.pc
    LICENSE
)
sha256sums=(
    f681aa518233bc407e23acf0f5887c884f17436f000d453b2491a9f11a52400c
    9b053d9e1227d3db3b14c4604b431d9664bdf7b88253caa7d08b9c2c07f17305
    8c57684fd87a731898f4d32a280901516c02d9109c9b5b945325aaca05b8c5dc
    142fb08b41a807b192b4b2c166696a1830a1c97967e5099ad0e579bf500e1da4
)

prepare() {
  cd lua-$pkgver
  patch -p1 -i ../liblua.so.patch

  sed -e "s/%VER%/$_majorver/g;s/%REL%/$pkgver/g" ../lua.pc > lua.pc
}

build() {
  cd lua-$pkgver

  # quite a lot of packages still use lua 5.1 API, enable 5.1 compat mode
  make MYCFLAGS="$CFLAGS -fPIC -DLUA_COMPAT_5_2 -DLUA_COMPAT_5_1" MYLDFLAGS="$LDFLAGS" macosx
}

package() {
  cd lua-$pkgver

  make \
    TO_LIB="liblua.a liblua.so liblua.so.$_majorver liblua.so.$pkgver" \
    INSTALL_TOP="$pkgdir"/opt/arch \
    INSTALL_MAN="$pkgdir"/opt/arch/share/man/man1 \
    install
  ln -sf lua "$pkgdir"/opt/arch/bin/lua$_majorver
  ln -sf luac "$pkgdir"/opt/arch/bin/luac$_majorver

  install -dm755 "$pkgdir"/opt/arch/lib/pkgconfig
  install -m644 lua.pc "$pkgdir"/opt/arch/lib/pkgconfig/${pkgname}53.pc
  ln -sf ${pkgname}53.pc "$pkgdir"/opt/arch/lib/pkgconfig/$pkgname.pc

  install -d "$pkgdir"/opt/arch/share/doc/$pkgname
  install -m644 doc/*.{gif,png,css,html} "$pkgdir"/opt/arch/share/doc/$pkgname
  install -dm755 "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 ../LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}
