pkgname=libffi
pkgver=3.2.1
pkgrel=2
pkgdesc='Portable foreign function interface library'
arch=('i686' 'x86_64')
url='http://sourceware.org/libffi/'
license=('MIT')
depends=('libc')
checkdepends=('dejagnu')
source=(ftp://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz)
sha1sums=('280c265b789e041c02e5c97815793dfc283fb1e6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/opt/arch --disable-static \
    --enable-pax_emutramp
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/opt/arch/share/licenses/$pkgname/LICENSE
}
