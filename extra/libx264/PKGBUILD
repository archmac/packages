pkgname=('x264' 'libx264' 'libx264-10bit' 'libx264-all')
pkgver=152.20171224
pkgrel=2
arch=('x86_64')
url='https://www.videolan.org/developers/x264.html'
license=('GPL')
#depends=('glibc')
makedepends=(
  'git'
#  'ffmpeg'
#  'l-smash'
  'nasm'
)
_commit='e9a5903edf8ca59ef20e6f4894c196f135af735e'
source=("git+https://git.videolan.org/git/x264.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgbase}

  local _ver=$(grep '#define X264_BUILD' x264.h | cut -d' ' -f3)
  local _date=$(git log -1 --format="%cd" --date=short | tr -d -)

  echo ${_ver}.${_date}
}

prepare() {
  mkdir -p build-{8,10}bit
}

build() {
  export PATH="/opt/arch/bin:/opt/arch/sbin:$PATH"

  for _b in 8 10; do (
    cd build-${_b}bit
    ../${pkgbase}/configure \
      --prefix='/opt/arch' \
      --disable-lsmash \
      --enable-shared \
      --enable-static \
      --enable-strip \
      --enable-lto \
      --bit-depth="${_b}"
    make
  ) done
}

package_x264() {
  pkgdesc='CLI tools for encoding H264/AVC video streams'
  #depends+=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'liblsmash.so'
  #          'libswscale.so')

  for _b in {8,10}bit; do
    provides+=("x264-${_b}")

    make -C build-${_b} DESTDIR="${pkgdir}" install-cli
    mv "${pkgdir}"/opt/arch/bin/x264{,-${_b}}
  done

  ln -s x264-8bit "${pkgdir}"/opt/arch/bin/x264
}

package_libx264() {
  pkgdesc='Library for encoding H264/AVC video streams (8bit depth)'
  provides=('libx264-8bit' 'libx264')
  conflicts=('libx264')

  make -C build-8bit DESTDIR=${pkgdir} install-lib-shared
}

package_libx264-10bit() {
  pkgdesc='Library for encoding H264/AVC video streams (10bit depth)'
  provides=('libx264-10bit' 'libx264')
  conflicts=('libx264')

  make -C build-10bit DESTDIR=${pkgdir} install-lib-shared
}

package_libx264-all() {
  pkgdesc="Library for encoding H264/AVC video streams (all depths)"

  local _ver=$(grep '#define X264_BUILD' "${pkgbase}"/x264.h | cut -d' ' -f3)

  install -d "${pkgdir}"/opt/arch/lib/x264

  for _b in {8,10}bit; do
    provides+=("libx264-${_b}")

    make -C build-${_b} DESTDIR="${pkgdir}" install-lib-shared

    mv "${pkgdir}"/opt/arch/lib/libx264.${_ver}.dylib "${pkgdir}"/opt/arch/lib/x264/libx264-${_b}.${_ver}.dylib
    rm -r "${pkgdir}"/opt/arch/{include,lib/libx264.dylib,lib/pkgconfig}

    ln -s libx264-${_b}.${_ver}.dylib "${pkgdir}"/opt/arch/lib/x264/libx264-${_b}.dylib
  done
}

# vim: ts=2 sw=2 et:
