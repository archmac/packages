pkgname=python3
pkgver=3.5.1
pkgrel=3
_pybasever=3.5
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64')
license=('custom')
url="http://www.python.org/"
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
#makedepends=('tk' 'sqlite' 'valgrind' 'bluez-libs' 'mpdecimal')
makedepends=('sqlite')
checkdepends=('gdb')
optdepends=('python3-setuptools'
            'python3-pip'
            'sqlite'
            #'mpdecimal: for decimal'
            'xz: for lzma'
            'tk: for tkinter')
options=('!makeflags')
source=("http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"
        dont-make-libpython-readonly.patch)
sha1sums=('0186da436db76776196612b98bb9c2f76acfe90e'
          'c22b24324b8e53326702de439c401d97927ee3f2')

prepare() {
  cd Python-${pkgver}

  # FS#45809
  patch -p1 -i ../dont-make-libpython-readonly.patch

  # FS#23997
  #sed -i -e "s|^#.* /opt/arch/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib, libffi, and libmpdec),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  #rm -r Modules/_ctypes/{darwin,libffi}*
  #rm -r Modules/_decimal/libmpdec
}

build() {
  cd Python-${pkgver}

  # Disable bundled pip & setuptools
  ./configure --prefix=/opt/arch \
              --without-gcc \
              --with-threads \
              --with-computed-gotos \
              --enable-ipv6 \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --enable-loadable-sqlite-extensions \
              --without-ensurepip \
              --enable-shared \
              #--with-system-ffi \
              #--with-system-libmpdec \
              #--enable-framework=/Library/Frameworks \

  make EXTRA_CFLAGS="$CFLAGS"
}

package() {
  cd Python-${pkgver}
  make DESTDIR="${pkgdir}" EXTRA_CFLAGS="$CFLAGS" install maninstall

  # Fix FS#22552
  ln -sf ../../libpython${_pybasever}m.so \
    "${pkgdir}/opt/arch/lib/python${_pybasever}/config-${_pybasever}m/libpython${_pybasever}m.so"

  # some useful "stuff" FS#46146
  install -dm755 "${pkgdir}"/opt/arch/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/opt/arch/lib/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} "${pkgdir}"/opt/arch/lib/python${_pybasever}/Tools/scripts/

  # Clean-up reference to build directory
  sed -i -e "s|$srcdir/Python-${pkgver}:||" "$pkgdir/opt/arch/lib/python${_pybasever}/config-${_pybasever}m/Makefile"

  # License
  install -dm755 "${pkgdir}/opt/arch/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/opt/arch/share/licenses/${pkgname}/LICENSE"
}
