pkgname=lua51
pkgver=5.1.5
pkgrel=6
pkgdesc='Powerful lightweight programming language designed for extending applications'
arch=('x86_64')
url='http://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=(
    http://www.lua.org/ftp/lua-$pkgver.tar.gz
    lua-arch.patch
    lua-5.1-cflags.diff
)
sha256sums=(
    '2640fc56a795f29d28ef15e13c34a47e223960b0240e8cb0a82d9b0738695333'
    'f30597a8afc458a29fc04d94fe7f93e433ad76be586a43c7da05e9ace1801ad3'
    '2eccdd4a0e4b1b9bfd4b45a018e8cd6105482646366c5192316814100fa46d9a'
)

prepare() {
  cd lua-$pkgver
  patch -p1 -i ../lua-arch.patch
  patch -p1 -i ../lua-5.1-cflags.diff

  sed -i~ -e 's:llua:llua5.1:' -e 's:/include:/include/lua5.1:' etc/lua.pc
  sed -i~ -e '/^LUA_SO=/ s/lua/lua5.1/' -e '/^LUA_A=/ s/lua/lua5.1/' -e '/^LUA_T=/ s/lua/lua5.1/' -e '/^LUAC_T=/ s/luac/luac5.1/' src/Makefile
}

build() {
  cd lua-$pkgver

  make MYCFLAGS="$CFLAGS -fPIC" MYLDFLAGS="$LDFLAGS" macosx
}

package() {
  cd lua-$pkgver

  make \
    TO_BIN='lua5.1 luac5.1' \
    TO_LIB="liblua5.1.a liblua5.1.so liblua5.1.so.5.1 liblua5.1.so.$pkgver" \
    INSTALL_TOP="$pkgdir"/opt/arch \
    INSTALL_INC="$pkgdir"/opt/arch/include/lua5.1 \
    INSTALL_MAN="$pkgdir"/opt/arch/share/man/man1 \
    install

  install -dm755 "$pkgdir"/opt/arch/lib/pkgconfig
  install -m644 etc/lua.pc "$pkgdir"/opt/arch/lib/pkgconfig/$pkgname.pc

  install -d "$pkgdir"/opt/arch/share/doc/$pkgname
  install -m644 doc/*.{gif,png,css,html} "$pkgdir"/opt/arch/share/doc/$pkgname
  install -m755 -d "$pkgdir"/opt/arch/share/licenses/$pkgname
  install -m644 COPYRIGHT "$pkgdir"/opt/arch/share/licenses/$pkgname/COPYRIGHT

  ln -s liblua5.1.so "$pkgdir"/opt/arch/lib/liblua.so.5.1
  ln -s liblua5.1.so "$pkgdir"/opt/arch/lib/liblua.so.$pkgver

  cd "$pkgdir"/opt/arch/share/man/man1
  mv lua.1 lua5.1.1
  mv luac.1 luac5.1.1
}
